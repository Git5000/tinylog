apply plugin: "com.android.application"
apply plugin: "jacoco"
apply plugin: "de.mannodermaus.android-junit5"

buildscript {
    repositories {
        google()
        jcenter()
        maven { url = uri("https://plugins.gradle.org/m2/") }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.6.2.0"
        classpath "org.jacoco:org.jacoco.core:0.8.5"
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:1.2.0"
    androidTestImplementation "org.assertj:assertj-core:3.16.1"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0-RC1"
    androidTestImplementation "org.junit-pioneer:junit-pioneer:0.9.0"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:1.2.0"
}

android {
    buildToolsVersion "29.0.3"
    compileSdkVersion 29

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    defaultConfig {
        applicationId "org.tinylog.core.android"
        minSdkVersion 26
        targetSdkVersion 29
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument("runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder")
    }

    packagingOptions {
        exclude("META-INF/LICENSE.md")
        exclude("META-INF/LICENSE-notice.md")
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
            jacoco {
                destinationFile = file("$buildDir/jacoco/moduleTestsCoverage.exec")
                includeNoLocationClasses = true
                excludes = ["jdk.internal.*"]
            }
        }
    }

    sourceSets {
        main.java.srcDirs += "../tinylog-core-base/src/main/java"
        main.resources.srcDirs += "../tinylog-core-base/src/main/resources"

        androidTest {
            java.srcDirs += "../tinylog-core-base/src/test/java"
            resources.srcDirs += "../tinylog-core-base/src/test/resources"
        }
    }
}

jacoco {
    toolVersion "0.8.5"
}
